{-
Simple parser for a SQL-like language

Implemented queries:
- SELECT
- CREATE STREAM
- INSERT

It can parse the following file content:

```
SELECT AVG(s1.aaa) AS agg, b.bb[2].bbb, c, stream2.ddd[1]
FROM stream1 AS s1 LEFT JOIN stream2
                   WITHIN (INTERVAL 10 SECOND)
                   ON s1.id = stream2.id
WHERE stream1.f1 > stream2.f2 AND s1.f2 <= 10 OR stream2.f3 <> "xxx"
GROUP BY name, SESSION(INTERVAL 2 MINUTE, INTERVAL 30 SECOND)
HAVING agg >= 10;

CREATE STREAM stream_1 WITH (SOURCE = "topic_source", FORMAT = "json");

CREATE STREAM stream_2 AS SELECT * FROM stream_1 WITH (SOURCE = "topic_sink", FORMAT = "json");

INSERT INTO stream_1 VALUES (1, 2020-01-01, [1, 2, 3], {xxx1: "aaa", xxx2: 114.514});
```
-}

--------------------------------------------------------------------------------

comment "//" ;
comment "/*" "*/" ;

QSelect. SQL ::= Select ";" ;
QCreate. SQL ::= Create ";" ;
QInsert. SQL ::= Insert ";" ;

---- Create Query
DCreate.  Create ::= "CREATE" "STREAM" Ident "WITH" "(" [StreamOption] ")" ;
separator StreamOption "," ;
CreateAs. Create ::= "CREATE" "STREAM" Ident "AS" Select "WITH" "(" [StreamOption] ")" ;

-- OptionTopic.  StreamOption ::= "TOPIC"  "=" String ;
OptionFormat. StreamOption ::= "FORMAT" "=" String ;

---- Insert Query
DInsert. Insert ::= "INSERT" "INTO" Ident "(" [Ident] ")" "VALUES" "(" [ValueExpr] ")" ;
separator Ident "," ;
separator ValueExpr "," ;

---- Select Query
DSelect. Select ::= Sel From Where GroupBy Having ;

-- Sel
DSel. Sel ::= "SELECT" SelList ;
SelListAsterisk. SelList ::= "*" ;
SelListSublist.  SelList ::= [DerivedCol] ;
separator DerivedCol "," ;

DerivedColSimpl. DerivedCol ::= ValueExpr ;
DerivedColAs.    DerivedCol ::= ValueExpr "AS" Ident ;

-- From
DFrom. From ::= "FROM" [TableRef] ;
separator TableRef "," ;

TableRefSimple. TableRef ::= Ident ;
TableRefAs.     TableRef ::= TableRef "AS" Ident ;
TableRefJoin.   TableRef ::= TableRef JoinType "JOIN" TableRef JoinWindow JoinCond ;

JoinLeft.  JoinType ::= "LEFT" ;
JoinRight. JoinType ::= "RIGHT" ;
JoinFull.  JoinType ::= "FULL" ;
JoinCross. JoinType ::= "CROSS" ;

DJoinWindow. JoinWindow ::= "WITHIN" "(" Interval ")" ;
DJoinCond. JoinCond ::= "ON" SearchCond ;

-- Where
DWhereEmpty. Where ::= "" ;
DWhere.     Where ::= "WHERE" SearchCond ;

-- GroupBy
DGroupByEmpty. GroupBy ::= "" ;
DGroupBy.      GroupBy ::= "GROUP" "BY" [GrpItem] ;
separator GrpItem "," ;

GrpItemCol. GrpItem ::= ColName ;
GrpItemWin. GrpItem ::= Window ;

TumblingWindow. Window ::= "TUMBLING" "(" Interval ")" ;
HoppingWindow.  Window ::= "HOPPING"  "(" Interval "," Interval ")" ;
SessionWindow.  Window ::= "SESSION"  "(" Interval ")" ;

-- Having
DHavingEmpty. Having ::= "" ;
DHaving.      Having ::= "HAVING" SearchCond ;

--------------------------------------------------------------------------------

---- Value Expression
-- Arith Expr
ExprAdd. ValueExpr  ::= ValueExpr  "+" ValueExpr1 ;
ExprSub. ValueExpr  ::= ValueExpr  "-" ValueExpr1 ;
ExprMul. ValueExpr1 ::= ValueExpr1 "*" ValueExpr2 ;

-- Scalar Numeric Type
ExprInt. ValueExpr2 ::= Integer ;
ExprNum. ValueExpr2 ::= Double  ;

-- Plain String
ExprString. ValueExpr2 ::= String ;

-- Date, Time and Interval
DDate. Date ::= "DATE" Integer "-" Integer "-" Integer ;
DTime. Time ::= "TIME" Integer ":" Integer ":" Integer ;
ExprDate. ValueExpr2 ::= Date ;
ExprTime. ValueExpr2 ::= Time ;

TimeUnitYear.  TimeUnit ::= "YEAR"   ;
TimeUnitMonth. TimeUnit ::= "MONTH"  ;
TimeUnitWeek.  TimeUnit ::= "WEEK"   ;
TimeUnitDay.   TimeUnit ::= "DAY"    ;
TimeUnitMin.   TimeUnit ::= "MINUTE" ;
TimeUnitSec.   TimeUnit ::= "SECOND" ;

DInterval. Interval ::= "INTERVAL" Integer TimeUnit ;
ExprInterval. ValueExpr2 ::= Interval ;

-- Non-Scalar Type (Array and Map)
ExprArr. ValueExpr ::= "[" [ValueExpr] "]" ;
ExprMap. ValueExpr ::= "{" [LabelledValueExpr] "}" ;
separator LabelledValueExpr "," ;

DLabelledValueExpr. LabelledValueExpr ::= Ident ":" ValueExpr ;

-- Column Name
ColNameSimple. ColName ::= Ident ;
ColNameStream. ColName ::= Ident "." Ident ;
ColNameInner.  ColName ::= ColName "[" Ident "]" ;
ColNameIndex.  ColName ::= ColName "[" Integer "]" ;
ExprColName. ValueExpr2 ::= ColName ;

-- Set Functions
SetFuncCountAll. SetFunc ::= "COUNT(*)" ;
SetFuncCount.    SetFunc ::= "COUNT" "(" ValueExpr ")" ;
SetFuncAvg.      SetFunc ::= "AVG"   "(" ValueExpr ")" ;
SetFuncSum.      SetFunc ::= "SUM"   "(" ValueExpr ")" ;
SetFuncMax.      SetFunc ::= "MAX"   "(" ValueExpr ")" ;
SetFuncMin.      SetFunc ::= "MIN"   "(" ValueExpr ")" ;
ExprSetFunc. ValueExpr2 ::= SetFunc ;

coercions ValueExpr 2 ;


---- Search Condition
CondOr.  SearchCond  ::= SearchCond  "OR"  SearchCond1 ;
CondAnd. SearchCond1 ::= SearchCond1 "AND" SearchCond2 ;
CondNot. SearchCond2 ::= "NOT" SearchCond3 ;
CondOp.  SearchCond3 ::= ValueExpr CompOp ValueExpr ;
CondBetween. SearchCond3 ::= ValueExpr "BETWEEN" ValueExpr "AND" ValueExpr ;

coercions SearchCond 3 ;

CompOpEQ.  CompOp ::= "=" ;
CompOpNE.  CompOp ::= "<>" ;
CompOpLT.  CompOp ::= "<" ;
CompOpGT.  CompOp ::= ">" ;
CompOpLEQ. CompOp ::= "<=" ;
CompOpGEQ. CompOp ::= ">=" ;
