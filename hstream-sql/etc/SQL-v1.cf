{-
Simple parser for a SQL-like language
-}

--------------------------------------------------------------------------------

PInteger.  PNInteger ::= "+" Integer ;
IPInteger. PNInteger ::=     Integer ;
NInteger.  PNInteger ::= "-" Integer ;

PDouble.  PNDouble ::= "+" Double ;
IPDouble. PNDouble ::=     Double ;
NDouble.  PNDouble ::= "-" Double ;

token SString '\'' char+ '\'' ;
token QuotedRaw '`' (char - ["`"])+ '`' ;

ColumnIdentNormal. ColumnIdent ::= Ident     ;
ColumnIdentRaw.    ColumnIdent ::= QuotedRaw ;

HIdentNormal. HIdent ::= Ident     ;
HIdentRaw.    HIdent ::= QuotedRaw ;
--------------------------------------------------------------------------------

comment "//" ;
comment "/*" "*/" ;

entrypoints SQL ;

QSelect.      SQL ::= Select      ";" ;
QPushSelect.  SQL ::= Select "EMIT" "CHANGES" ";" ;
QCreate.      SQL ::= Create      ";" ;
QInsert.      SQL ::= Insert      ";" ;
QShow.        SQL ::= ShowQ       ";" ;
QDrop.        SQL ::= Drop        ";" ;
QTerminate.   SQL ::= Terminate   ";" ;
QExplain.     SQL ::= Explain     ";" ;
QPause.       SQL ::= Pause       ";" ;
QResume.      SQL ::= Resume      ";" ;

---- Create Query
DCreate.  Create ::= "CREATE" "STREAM" HIdent ;
CreateOp. Create ::= "CREATE" "STREAM" HIdent "WITH" "(" [StreamOption] ")" ;
separator StreamOption "," ;
CreateAs. Create ::= "CREATE" "STREAM" HIdent "AS" Select ;
CreateAsOp. Create ::= "CREATE" "STREAM" HIdent "AS" Select "WITH" "(" [StreamOption] ")" ;

-- OptionTopic.  StreamOption ::= "TOPIC"  "=" String ;
-- OptionFormat. StreamOption ::= "FORMAT" "=" String ;
OptionRepFactor. StreamOption ::= "REPLICATE" "=" PNInteger ;
OptionDuration.  StreamOption ::= "DURATION" "=" Interval ;

CreateSourceConnector. Create ::= "CREATE" "SOURCE" "CONNECTOR" HIdent "FROM" HIdent "WITH" "(" [ConnectorOption] ")" ;
CreateSourceConnectorIf. Create ::= "CREATE" "SOURCE" "CONNECTOR" HIdent "FROM" HIdent "IF" "NOT" "EXIST" "WITH" "(" [ConnectorOption] ")" ;
CreateSinkConnector. Create ::= "CREATE" "SINK" "CONNECTOR" HIdent "TO" HIdent "WITH" "(" [ConnectorOption] ")" ;
CreateSinkConnectorIf. Create ::= "CREATE" "SINK" "CONNECTOR" HIdent "TO" HIdent "IF" "NOT" "EXIST" "WITH" "(" [ConnectorOption] ")" ;
separator ConnectorOption "," ;

CreateView. Create ::= "CREATE" "VIEW" HIdent "AS" Select ;

ConnectorProperty. ConnectorOption ::= String "=" ValueExpr ;

---- Pause Query
PauseConnector. Pause ::= "PAUSE" "CONNECTOR" HIdent ;
PauseQuery.     Pause ::= "PAUSE" "QUERY" HIdent ;

--- Resume Query
ResumeConnector. Resume ::= "RESUME" "CONNECTOR" HIdent ;
ResumeQuery.    Resume  ::= "RESUME" "QUERY" HIdent ;

---- Insert Query
DInsert. Insert ::= "INSERT" "INTO" HIdent "(" [ColumnIdent] ")" "VALUES" "(" [ValueExpr] ")" ;
separator ColumnIdent "," ;
separator ValueExpr "," ;
InsertBinary. Insert ::= "INSERT" "INTO" HIdent "VALUES" String ;
InsertJson.   Insert ::= "INSERT" "INTO" HIdent "VALUES" SString ;

---- Show Query
DShow. ShowQ ::= "SHOW" ShowOption ;
ShowQueries. ShowOption ::= "QUERIES" ;
ShowStreams. ShowOption ::= "STREAMS" ;
ShowConnectors. ShowOption ::= "CONNECTORS" ;
ShowViews. ShowOption ::= "VIEWS" ;

--- Drop Query
DDrop.  Drop ::= "DROP" DropOption HIdent ;
DropIf. Drop ::= "DROP" DropOption HIdent "IF" "EXISTS" ;

DropConnector. DropOption ::= "CONNECTOR" ;
DropStream.    DropOption ::= "STREAM"    ;
DropView.      DropOption ::= "VIEW"      ;
DropQuery.     DropOption ::= "QUERY"     ;

-- Terminate Query
TerminateQuery. Terminate ::= "TERMINATE" "QUERY" HIdent ;

---- Explain
ExplainSelect. Explain ::= "EXPLAIN" Select ;
ExplainCreate. Explain ::= "EXPLAIN" Create ;

---- Select Query
SelectFromView. Select ::= Sel From Where GroupBy Having ;
SelectSimple.   Select ::= Sel                           ;

-- Sel
DSel. Sel ::= "SELECT" [SelectItem] ;
separator SelectItem "," ;

SelectItemUnnamedExpr.       SelectItem ::= ValueExpr ;
SelectItemExprWithAlias.     SelectItem ::= ValueExpr "AS" ColumnIdent ;
SelectItemQualifiedWildcard. SelectItem ::= HIdent "." "*" ;
SelectItemWildcard.          SelectItem ::= "*" ;

-- From
DFrom. From ::= "FROM" TableRef;

_. TableRef ::= TableRef1;
_. TableRef ::= TableRef3;
_. TableRef1 ::= TableRef2;

TableRefCrossJoin.   TableRef1 ::= TableRef1 JoinTypeWithoutCond "JOIN" TableRef2 "WITHIN" "(" Interval ")" ;
TableRefNaturalJoin. TableRef1 ::= TableRef1 "NATURAL" JoinTypeWithCond "JOIN" TableRef2 "WITHIN" "(" Interval ")" ;
TableRefJoinOn.      TableRef1 ::= TableRef1 JoinTypeWithCond "JOIN" TableRef2 "ON" ValueExpr "WITHIN" "(" Interval ")" ;
TableRefJoinUsing.   TableRef1 ::= TableRef1 JoinTypeWithCond "JOIN" TableRef2 "USING" "(" [ColName] ")" "WITHIN" "(" Interval ")" ;

TableRefTumbling. TableRef3 ::= "TUMBLE" "(" HIdent "," Interval ")" ;
TableRefHopping.  TableRef3 ::= "HOP"    "(" HIdent "," Interval "," Interval ")" ;
TableRefSession.  TableRef3 ::= "SESSION"  "(" HIdent "," Interval ")" ;

TableRefAs.    TableRef2 ::= HIdent "AS" HIdent ;
TableRefIdent. TableRef2 ::= HIdent ;

-- TableRefSubquery.  TableRef2 ::= "(" Select ")" ;

separator ColName "," ;

JoinInner1. JoinTypeWithCond ::= "INNER" ;
JoinInner2. JoinTypeWithCond ::= "" ;
JoinLeft1.  JoinTypeWithCond ::= "LEFT" ;
JoinLeft2.  JoinTypeWithCond ::= "LEFT" "OUTER" ;
JoinRight1. JoinTypeWithCond ::= "RIGHT" ;
JoinRight2. JoinTypeWithCond ::= "RIGHT" "OUTER" ;
JoinFull1.  JoinTypeWithCond ::= "FULL" ;
JoinFull2.  JoinTypeWithCond ::= "FULL" "OUTER" ;
JoinCross.  JoinTypeWithoutCond ::= "CROSS" ;

-- Where
DWhereEmpty. Where ::= "" ;
DWhere.     Where ::= "WHERE" ValueExpr ;

-- GroupBy

-- DTumbling. TimeWindow ::= "TUMBLING" "(" Interval ")" ;
-- DHopping.  TimeWindow ::= "HOPPING"  "(" Interval "," Interval ")" ;
-- DSession.  TimeWindow ::= "SESSION"  "(" Interval ")" ;

DGroupByEmpty. GroupBy ::= "" ;
DGroupBy.      GroupBy ::= "GROUP" "BY" [ColName] ;
-- DGroupByWin.   GroupBy ::= "GROUP" "BY" [ColName] TimeWindow ;

-- Having
DHavingEmpty. Having ::= "" ;
DHaving.      Having ::= "HAVING" ValueExpr ;

--------------------------------------------------------------------------------

---- Data Types
TypeInteger.  DataType ::= "INTEGER" ;
TypeFloat.    DataType ::= "FLOAT" ;
TypeBoolean.  DataType ::= "BOOLEAN" ;
TypeByte.     DataType ::= "BYTEA" ;
TypeText.     DataType ::= "STRING" ;
TypeDate.     DataType ::= "DATE" ;
TypeTime.     DataType ::= "TIME" ;
TypeTimestamp. DataType ::= "TIMESTAMP" ;
TypeInterval. DataType ::= "INTERVAL" ;
TypeJson.     DataType ::= "JSONB" ;
TypeArray.    DataType ::= DataType "[]" ;

--------------------------------------------------------------------------------

---- Value Expression
-- Boolean Expr
ExprOr.  ValueExpr  ::= ValueExpr  "OR" ValueExpr1 ;
ExprAnd. ValueExpr1 ::= ValueExpr1 "AND" ValueExpr2 ;

ExprEQ.  ValueExpr2 ::= ValueExpr2 "="  ValueExpr3 ;
ExprNEQ. ValueExpr2 ::= ValueExpr2 "<>" ValueExpr3 ;
ExprLT.  ValueExpr2 ::= ValueExpr2 "<"  ValueExpr3 ;
ExprGT.  ValueExpr2 ::= ValueExpr2 ">"  ValueExpr3 ;
ExprLEQ. ValueExpr2 ::= ValueExpr2 "<=" ValueExpr3 ;
ExprGEQ. ValueExpr2 ::= ValueExpr2 ">=" ValueExpr3 ;

-- Json Access
ExprScalarFunc. ValueExpr3 ::= ScalarFunc ;

-- Arith Expr
ExprAdd. ValueExpr4 ::= ValueExpr4 "+" ValueExpr5 ;
ExprSub. ValueExpr4 ::= ValueExpr4 "-" ValueExpr5 ;
ExprMul. ValueExpr5 ::= ValueExpr5 "*" ValueExpr6 ;

-- Array Access
ArrayAccessRhsIndex. ArrayAccessRhs ::= "[" Integer "]" ;
ArrayAccessRhsFrom. ArrayAccessRhs ::= "[" Integer ":" "]" ;
ArrayAccessRhsTo. ArrayAccessRhs ::= "[" ":" Integer "]" ;
ArrayAccessRhsFromTo. ArrayAccessRhs ::= "[" Integer ":" Integer "]" ;

ExprAccessArray. ValueExpr6 ::= ValueExpr7 ArrayAccessRhs ;

-- Type Casting
ExprCast1. ValueExpr7 ::= ValueExpr8 "::" DataType ;
ExprCast2. ValueExpr7 ::= "CAST" "(" ValueExpr "AS" DataType ")" ;

-- Null Expr.
ExprNull. ValueExpr8 ::= "NULL" ;

-- Scalar Numeric Type
ExprInt. ValueExpr8 ::= PNInteger ; -- integer
ExprNum. ValueExpr8 ::= PNDouble  ; -- float

-- Plain String
ExprString. ValueExpr8 ::= String ; -- string

-- Boolean Type
BoolTrue.  Boolean ::= "TRUE"  ; -- boolean
BoolFalse. Boolean ::= "FALSE" ;
ExprBool. ValueExpr8 ::= Boolean ;

-- Date, Time and Interval
TimeStrWithoutMicroSec. TimeStr ::= Integer ":" Integer ":" Integer ;
TimeStrWithMicroSec.    TimeStr ::= Integer ":" Integer ":" Integer "." Integer ;

DDateStr. DateStr ::= Integer "-" Integer "-" Integer ;
DDateTimeStr. DateTimeStr ::= DateStr TimeStr ;

TimezoneZ.        Timezone ::= "Z" ;
TimezonePositive. Timezone ::= "+" Integer ":" Integer ;
TimezoneNegative. Timezone ::= "-" Integer ":" Integer ;

DTimestampStr. TimestampStr ::= DateStr "T" TimeStr Timezone ;

DDate. Date ::= "DATE" DateStr ;
DTime. Time ::= "TIME" TimeStr ;
TimestampWithoutZone. Timestamp ::= "TIMESTAMP" DateTimeStr ;
TimestampWithZone.    Timestamp ::= "TIMESTAMP" TimestampStr ;

ExprDate. ValueExpr8 ::= Date ;
ExprTime. ValueExpr8 ::= Time ;
ExprTimestamp. ValueExpr8 ::= Timestamp ;

IntervalSecond. IntervalUnit ::= "SECOND" ;
IntervalMinute. IntervalUnit ::= "MINUTE" ;
IntervalHour.   IntervalUnit ::= "HOUR"   ;
IntervalDay.    IntervalUnit ::= "DAY"    ;
IntervalMonth.  IntervalUnit ::= "MONTH"  ;
IntervalYear.   IntervalUnit ::= "YEAR"   ;

Interval. Interval ::= "INTERVAL" SString IntervalUnit ;
ExprInterval. ValueExpr8 ::= Interval ;

-- Non-Scalar Type (Array)
ExprArr. ValueExpr ::= "[" [ValueExpr] "]" ; -- array

-- SubQuery
-- ExprSubquery. ValueExpr8 ::= "(" Select ")" ;

-- Column Name
ColNameSimple. ColName ::= ColumnIdent ;
ColNameStream. ColName ::= HIdent "." ColumnIdent ;
ExprColName. ValueExpr8 ::= ColName ;

-- Set Functions
SetFuncCountAll. SetFunc ::= "COUNT" "(" "*"       ")" ;
SetFuncCount.    SetFunc ::= "COUNT" "(" ValueExpr ")" ;
SetFuncAvg.      SetFunc ::= "AVG"   "(" ValueExpr ")" ;
SetFuncSum.      SetFunc ::= "SUM"   "(" ValueExpr ")" ;
SetFuncMax.      SetFunc ::= "MAX"   "(" ValueExpr ")" ;
SetFuncMin.      SetFunc ::= "MIN"   "(" ValueExpr ")" ;

SetFuncTopK.         SetFunc ::= "TOPK"         "(" ValueExpr "," ValueExpr ")" ;
SetFuncTopKDistinct. SetFunc ::= "TOPKDISTINCT" "(" ValueExpr "," ValueExpr ")" ;

ExprSetFunc. ValueExpr8 ::= SetFunc ;

-- Scalar Functions
-- jsonb related
ScalarFuncFieldToJson.   ScalarFunc ::= ValueExpr3 "->"  ValueExpr4 ;
ScalarFuncFieldToText.   ScalarFunc ::= ValueExpr3 "->>" ValueExpr4 ;
ScalarFuncFieldsToJson.  ScalarFunc ::= ValueExpr3 "#>"  ValueExpr4 ;
ScalarFuncFieldsToTexts. ScalarFunc ::= ValueExpr3 "#>>" ValueExpr4 ;

ScalarFuncSin.   ScalarFunc ::= "SIN"   "(" ValueExpr ")" ;
ScalarFuncSinh.  ScalarFunc ::= "SINH"  "(" ValueExpr ")" ;
ScalarFuncAsin.  ScalarFunc ::= "ASIN"  "(" ValueExpr ")" ;
ScalarFuncAsinh. ScalarFunc ::= "ASINH" "(" ValueExpr ")" ;
ScalarFuncCos.   ScalarFunc ::= "COS"   "(" ValueExpr ")" ;
ScalarFuncCosh.  ScalarFunc ::= "COSH"  "(" ValueExpr ")" ;
ScalarFuncAcos.  ScalarFunc ::= "ACOS"  "(" ValueExpr ")" ;
ScalarFuncAcosh. ScalarFunc ::= "ACOSH" "(" ValueExpr ")" ;
ScalarFuncTan.   ScalarFunc ::= "TAN"   "(" ValueExpr ")" ;
ScalarFuncTanh.  ScalarFunc ::= "TANH"  "(" ValueExpr ")" ;
ScalarFuncAtan.  ScalarFunc ::= "ATAN"  "(" ValueExpr ")" ;
ScalarFuncAtanh. ScalarFunc ::= "ATANH" "(" ValueExpr ")" ;

ScalarFuncAbs.   ScalarFunc ::= "ABS"   "(" ValueExpr ")" ;
ScalarFuncCeil.  ScalarFunc ::= "CEIL"  "(" ValueExpr ")" ;
ScalarFuncFloor. ScalarFunc ::= "FLOOR" "(" ValueExpr ")" ;
ScalarFuncRound. ScalarFunc ::= "ROUND" "(" ValueExpr ")" ;

ScalarFuncSign.  ScalarFunc ::= "SIGN"  "(" ValueExpr ")" ;
ScalarFuncSqrt.  ScalarFunc ::= "SQRT"  "(" ValueExpr ")" ;
ScalarFuncLog.   ScalarFunc ::= "LOG"   "(" ValueExpr ")" ;
ScalarFuncLog2.  ScalarFunc ::= "LOG2"  "(" ValueExpr ")" ;
ScalarFuncLog10. ScalarFunc ::= "LOG10" "(" ValueExpr ")" ;
ScalarFuncExp.   ScalarFunc ::= "EXP"   "(" ValueExpr ")" ;

ScalarFuncIsInt.   ScalarFunc ::= "IS_INT"   "(" ValueExpr ")" ;
ScalarFuncIsFloat. ScalarFunc ::= "IS_FLOAT" "(" ValueExpr ")" ;
ScalarFuncIsNum.   ScalarFunc ::= "IS_NUM"   "(" ValueExpr ")" ;
ScalarFuncIsBool.  ScalarFunc ::= "IS_BOOL"  "(" ValueExpr ")" ;
ScalarFuncIsStr.   ScalarFunc ::= "IS_STR"   "(" ValueExpr ")" ;
ScalarFuncIsArr.   ScalarFunc ::= "IS_ARRAY" "(" ValueExpr ")" ;
ScalarFuncIsDate.  ScalarFunc ::= "IS_DATE"  "(" ValueExpr ")" ;
ScalarFuncIsTime.  ScalarFunc ::= "IS_TIME"  "(" ValueExpr ")" ;

ScalarFuncToStr.  ScalarFunc ::= "TO_STR" "(" ValueExpr ")" ;

ScalarFuncToLower. ScalarFunc ::= "TO_LOWER"   "(" ValueExpr ")" ;
ScalarFuncToUpper. ScalarFunc ::= "TO_UPPER"   "(" ValueExpr ")" ;
ScalarFuncTrim.    ScalarFunc ::= "TRIM"       "(" ValueExpr ")" ;
ScalarFuncLTrim.   ScalarFunc ::= "LEFT_TRIM"  "(" ValueExpr ")" ;
ScalarFuncRTrim.   ScalarFunc ::= "RIGHT_TRIM" "(" ValueExpr ")" ;
ScalarFuncRev.     ScalarFunc ::= "REVERSE"    "(" ValueExpr ")" ;
ScalarFuncStrlen.  ScalarFunc ::= "STRLEN"     "(" ValueExpr ")" ;

ScalarFuncIfNull. ScalarFunc ::= "IFNULL" "(" ValueExpr "," ValueExpr ")" ;
ScalarFuncNullIf. ScalarFunc ::= "NULLIF" "(" ValueExpr "," ValueExpr ")" ;

ScalarFuncDateStr. ScalarFunc ::= "DATETOSTRING" "(" ValueExpr "," ValueExpr ")" ;
ScalarFuncStrDate. ScalarFunc ::= "STRINGTODATE" "(" ValueExpr "," ValueExpr ")" ;

ScalarFuncSplit.    ScalarFunc ::= "SPLIT"    "(" ValueExpr "," ValueExpr ")" ;
ScalarFuncChunksOf. ScalarFunc ::= "CHUNKSOF" "(" ValueExpr "," ValueExpr ")" ;

ScalarFuncTake.    ScalarFunc ::= "TAKE"    "(" ValueExpr "," ValueExpr ")" ;
ScalarFuncTakeEnd. ScalarFunc ::= "TAKEEND" "(" ValueExpr "," ValueExpr ")" ;
ScalarFuncDrop.    ScalarFunc ::= "DROP"    "(" ValueExpr "," ValueExpr ")" ;
ScalarFuncDropEnd. ScalarFunc ::= "DROPEND" "(" ValueExpr "," ValueExpr ")" ;

ArrayFuncContain.   ScalarFunc ::= "ARRAY_CONTAIN"   "(" ValueExpr "," ValueExpr ")" ;
ArrayFuncDistinct.  ScalarFunc ::= "ARRAY_DISTINCT"  "(" ValueExpr               ")" ;
ArrayFuncExcept.    ScalarFunc ::= "ARRAY_EXCEPT"    "(" ValueExpr "," ValueExpr ")" ;
ArrayFuncIntersect. ScalarFunc ::= "ARRAY_INTERSECT" "(" ValueExpr "," ValueExpr ")" ;
ArrayFuncLength.    ScalarFunc ::= "ARRAY_LENGTH"    "(" ValueExpr               ")" ;
ArrayFuncRemove.    ScalarFunc ::= "ARRAY_REMOVE"    "(" ValueExpr "," ValueExpr ")" ;
ArrayFuncUnion.     ScalarFunc ::= "ARRAY_UNION"     "(" ValueExpr "," ValueExpr ")" ;
ArrayFuncJoin.      ScalarFunc ::= "ARRAY_JOIN"      "(" ValueExpr               ")" ;
ArrayFuncJoinWith.  ScalarFunc ::= "ARRAY_JOIN"      "(" ValueExpr "," ValueExpr ")" ;
ArrayFuncMax.       ScalarFunc ::= "ARRAY_MAX"       "(" ValueExpr               ")" ;
ArrayFuncMin.       ScalarFunc ::= "ARRAY_MIN"       "(" ValueExpr               ")" ;
ArrayFuncSort.      ScalarFunc ::= "ARRAY_SORT"      "(" ValueExpr               ")" ;

coercions ValueExpr 8 ;
